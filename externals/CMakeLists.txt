cmake_minimum_required(VERSION 3.10)

# make cmake find our own provided libraries
# the libraries provided here have higher priority than system libraries
set(CAGE_EXTERNALS_MODULE_PATH "${CMAKE_CURRENT_BINARY_DIR}/cmake-modules")
set(CAGE_EXTERNALS_MODULE_PATH "${CAGE_EXTERNALS_MODULE_PATH}" PARENT_SCOPE)
list(INSERT CMAKE_MODULE_PATH 0 "${CAGE_EXTERNALS_MODULE_PATH}")
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(cmake/cage_gen_module.cmake)

# unset language standard
unset(CMAKE_C_STANDARD)
unset(CMAKE_CXX_STANDARD)

# unset visibility preset
unset(CMAKE_C_VISIBILITY_PRESET)
unset(CMAKE_CXX_VISIBILITY_PRESET)
unset(CMAKE_VISIBILITY_INLINES_HIDDEN)

# ensure that all libraries are distinguishable from system libraries
set(CMAKE_IMPORT_LIBRARY_PREFIX "${CMAKE_IMPORT_LIBRARY_PREFIX}cage-")
set(CMAKE_SHARED_LIBRARY_PREFIX "${CMAKE_SHARED_LIBRARY_PREFIX}cage-")
set(CMAKE_SHARED_MODULE_PREFIX "${CMAKE_SHARED_MODULE_PREFIX}cage-")

# no compilation warnings level
if(MSVC)
	foreach(conf IN ITEMS ${CMAKE_CONFIGURATION_TYPES} ${CMAKE_BUILD_TYPE})
		string(TOUPPER ${conf} conf_upper)
		string(REGEX REPLACE "/W[0-9]" "" CMAKE_C_FLAGS_${conf_upper} ${CMAKE_C_FLAGS_${conf_upper}})
		string(REGEX REPLACE "/W[0-9]" "" CMAKE_CXX_FLAGS_${conf_upper} ${CMAKE_CXX_FLAGS_${conf_upper}})
	endforeach(conf)
	string(REGEX REPLACE "/W[0-9]" "" CMAKE_C_FLAGS ${CMAKE_C_FLAGS})
	string(REGEX REPLACE "/W[0-9]" "" CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
endif()

add_subdirectory(zlib-ng)
add_subdirectory(libpng)
add_subdirectory(jpeg) # jpeg must go after libpng to ensure that asm compiler is detected and before zstd to ensure that no uninstall target is yet defined
add_subdirectory(zstd)
add_subdirectory(libtiff)
add_subdirectory(freetype)
add_subdirectory(assimp)
add_subdirectory(glad)
add_subdirectory(glm)
add_subdirectory(glfw)
add_subdirectory(cubeb)
add_subdirectory(dr_libs)
add_subdirectory(ogg)
add_subdirectory(vorbis)
add_subdirectory(utfcpp)
add_subdirectory(simplefilewatcher)
add_subdirectory(msdfgen)
add_subdirectory(xsimd)
add_subdirectory(fastnoise)
add_subdirectory(optick)
add_subdirectory(robin-hood-hashing)
add_subdirectory(hsluv)
add_subdirectory(stb)
add_subdirectory(xatlas)
add_subdirectory(pmp)
add_subdirectory(dualmc)
add_subdirectory(plf)
add_subdirectory(tinyexr)
add_subdirectory(samplerate)

add_dependencies(png_static zlib)
add_dependencies(jpeg-static zlib)
add_dependencies(tiff jpeg-static)
add_dependencies(freetype png_static)
add_dependencies(vorbis ogg)
add_dependencies(lib_msdfgen freetype)

# disable compilation warnings
function(disable_compilation_warnings target)
	if(MSVC)
		get_target_property(v ${target} COMPILE_FLAGS)
		if(v)
			string(REGEX REPLACE "/W[0-9]" "" v "${v}")
			set_target_properties(${target} PROPERTIES COMPILE_FLAGS "${v}")
		endif()
		get_target_property(v ${target} COMPILE_OPTIONS)
		if(v)
			list(FILTER v EXCLUDE REGEX "/W[0-9]")
			set_target_properties(${target} PROPERTIES COMPILE_OPTIONS "${v}")
		endif()
		target_compile_options(${target} PRIVATE "/W0")
	else()
		target_compile_options(${target} PRIVATE "-w")
	endif()
endfunction()

# target properties
function(external_target_properties target)
	if(NOT TARGET ${target})
		return()
	endif()
	disable_compilation_warnings(${target})
	set_target_properties(${target} PROPERTIES FOLDER cage/externals)
endfunction()

external_target_properties(zlib)
external_target_properties(libzstd_static)
external_target_properties(png_static)
external_target_properties(jpeg-static)
external_target_properties(tiff)
external_target_properties(tiffxx)
external_target_properties(freetype)
external_target_properties(assimp)
external_target_properties(IrrXML)
external_target_properties(glad)
external_target_properties(glfw)
external_target_properties(cubeb)
external_target_properties(dr_libs)
external_target_properties(ogg)
external_target_properties(vorbis)
external_target_properties(vorbisenc)
external_target_properties(vorbisfile)
external_target_properties(simplefilewatcher)
external_target_properties(lib_msdfgen)
external_target_properties(fastnoise)
external_target_properties(OptickCore)
external_target_properties(OptickGui)
external_target_properties(robin-hood-hashing)
external_target_properties(hsluv)
external_target_properties(stb)
external_target_properties(xatlas)
external_target_properties(pmp)
external_target_properties(dualmc)
external_target_properties(plf)
external_target_properties(tinyexr)
external_target_properties(samplerate)

# final overrides
set_target_properties(genfiles PROPERTIES FOLDER cage/externals)
if(TARGET simd)
	set_target_properties(simd PROPERTIES FOLDER cage/externals)
endif()
if(TARGET simd-objs)
	set_target_properties(simd-objs PROPERTIES FOLDER cage/externals)
endif()
if(TARGET OptickGui)
	set_target_properties(OptickGui PROPERTIES FOLDER cage/tools EXCLUDE_FROM_ALL false)
endif()

